apply from: file('gradle/project.gradle')

subprojects {
    apply from: file("${rootDir}/gradle/check.gradle")

    group = 'core.aws'
    version = '1.8.6'
}

def awsVersion = '1.11.95'
def junitJupiterVersion = '5.0.1'

project(':cmn') {
    apply plugin: 'application'
    mainClassName = 'Main'

    startScripts {
        defaultJvmOpts = ['-Dcmn.ansible=APP_HOME_VAR/ansible']

        doLast {
            windowsScript.text = windowsScript.text.replaceFirst('APP_HOME_VAR', '%APP_HOME%')
            unixScript.text = unixScript.text.replaceFirst('APP_HOME_VAR', '\\$APP_HOME')
        }
    }

    dependencies {
        compile "com.amazonaws:aws-java-sdk-autoscaling:${awsVersion}"
        compile "com.amazonaws:aws-java-sdk-ec2:${awsVersion}"
        compile "com.amazonaws:aws-java-sdk-elasticloadbalancing:${awsVersion}"
        compile "com.amazonaws:aws-java-sdk-cloudwatch:${awsVersion}"
        compile "com.amazonaws:aws-java-sdk-iam:${awsVersion}"
        compile "com.amazonaws:aws-java-sdk-s3:${awsVersion}"
        compile "com.amazonaws:aws-java-sdk-ecr:${awsVersion}"
        compile 'org.slf4j:slf4j-api:1.7.12'
        compile 'org.slf4j:jcl-over-slf4j:1.7.12'
        compile 'ch.qos.logback:logback-classic:1.1.3'
        compile 'com.jcraft:jsch:0.1.50'
        compile 'org.apache.commons:commons-compress:1.5'
        compile 'org.yaml:snakeyaml:1.13'

        testCompile "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
        testCompile 'org.hamcrest:java-hamcrest:2.0.0.0'
        testCompile 'org.mockito:mockito-core:2.11.0'
    }
}
